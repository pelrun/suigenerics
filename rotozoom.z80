
.texture_base: equ &c000

rows: equ 32
columns: equ 30

rightcols: equ 60-columns

IF REALNC100

txtwrchar: equ &b83c
txtsetcursor: equ &b836

org &6000

realncstart:
CALL map_scrn_in
CALL init_texture
CALL init_static
CALL spin
CALL reset_scrn
CALL map_scrn_out
RET

ELSE

org &100

emustart:
di
ld sp,&3fff
LD A,67
OUT (&13),A
CALL init_texture
CALL init_static
CALL spin
JP $

txtwrchar:
txtsetcursor:
ret

ENDIF

include 'trig.z80'

spin:
ld bc, 0
.spin_step
ld a, 1
add a,c
ld c,a
push bc

push bc
ld a,3
and c
call z, scroller
pop bc

ld hl,(.upos+1)
ld de,&00e0
add hl,de
ld (.upos+1),hl
ld hl,(.vpos+1)
ld de,&0025
add hl,de
ld (.vpos+1),hl

ld hl,sine_table
add hl,bc
call .rse
ld (.ustepy+1),de
call .neg
;sra d
;rr e
ld (.vstepx+1),de
ld hl,cosine_table
add hl,bc
call .rse
ld (.ustepx+1),de
sra d
rr e
ld (.vstepy+1),de
call rz_frame
;call flip_scrn
pop bc

in a,(&b0)
and &10 ; port &b0 bit &10 is enter key
ret nz

jp .spin_step

.rse:
ld a,(hl)
; convert 0:8 signed to 8:8 signed
sla a
ld e,a
sbc a, a
ld d, a
ret

.neg
xor a
sub e
ld e,a
sbc a,a
sub d
ld d,a
ret

map_scrn_in:

LD A,(&B003)
LD (.state),A
LD A,67
LD (&B003),A
OUT (&13),A
RET

map_scrn_out:

LD A,(.state)
LD (&B003),A
OUT (&13),A
RET

.state:

DEFB 0

flip_scrn:
LD IX,.scrn_base
LD A,(IX+3)
XOR &10
OUT (&00),A
LD (IX+3),A
RET

reset_scrn:
LD A,&f0
OUT (&00),A
RET

include 'syntax.z80'

init_texture:
ld hl, .texture_base
ld (hl),0
ld de, .texture_base+1 
ld bc, &4000 ;.texture_rows*0x100
ldir
ld de, .texture
ld hl, .texture_base
ld c, .texture_rows
.copy_row:
ld b, .texture_cols
ld l,0
.copy_col:
ld a,(de)
inc de
push bc
ld b,8
.expand_byte:
rlc a
jp nc,.white
ld (hl),0xff
.white:
inc l
djnz .expand_byte
pop bc
djnz .copy_col
inc h
dec c
jp nz, .copy_row
ret

rz_frame:
.upos:
ld hl,&0000 ; texture xpos
exx ; vpos
.vpos:
ld hl,.texture_base ; texture ypos
exx ; upos, count
.scrn_base:
ld ix,&f000
ld c,rows
.scrn:
ld b,columns
push hl ; save upos
.ustepx:
ld de,&0100 ; u/256 step per x step
exx ; vpos, tex
push hl ; save vpos
push ix ; save row start
.vstepx:
ld de,&0000 ; v/256 step per x step
exx ; upos, count
.nextchar:
add hl,de ; increment upos
ld a,h
and 63 ; wrap inside row
ld h,a
exx ; vpos, tex
add hl,de
ld c,a
ld a,15 ; wrap inside column
and h
or &c0 ; point at texture
ld h,a
ld b,h
ld a,(bc) ; read texel
ld (ix+0),a ; write to screen
inc ix
exx ; upos, count
djnz .nextchar
exx ; vpos, tex

; double scanline
pop de
ld hl,64
add hl,de
ex de,hl

push bc
ld bc,columns
ldir
pop bc

; move to next line
ld de,128-columns
add ix,de

pop hl
.vstepy:
ld de,&0020 ; v/256 step per y step
add hl,de
ld a,15
and h
or &c0
ld h,a
exx ; upos, count
pop hl
.ustepy:
ld de,&0000 ; u/256 step per y step
add hl,de
ld a,63
and h
ld h,a
dec c
jp nz, .scrn
ret

static_width: equ 224/8
static_height: equ 40
static_x: equ columns+(rightcols-static_width)/2
static_tex:
incbin 'static.bin'

write_static:
ld hl,static_tex
ld b,static_height
.writestaticrow:
push bc
push de
ld bc,static_width
ldir
pop de
ex de,hl
ld bc,64
add hl,bc
ex de,hl
pop bc
djnz .writestaticrow
ret

init_static:
ld de,&f000+static_x
call write_static
ld de,&e000+static_x
call write_static
ret

scroll_ptr:
defw scrolltext

defs +(512 - ($ & 511)) ; damn it why is there no alignment directive
scrolltext:
incbin 'scrolltext.txt'

MACRO next_char
inc hl
ld a,1
and h
or +(scrolltext>>8)
ld h,a
ENDM

scroller:

ld hl,0x2906
call txtsetcursor

ld hl,(scroll_ptr)
push hl

ld b, 38
.textout:
ld a,(hl)
call txtwrchar
next_char
djnz .textout

pop hl
next_char
ld (scroll_ptr),hl

ret
